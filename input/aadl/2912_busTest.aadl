------------------------------------------------------------------
--                 EXEMPLO DE BUS LOAD ANALYSIS                 --
------------------------------------------------------------------
package Exemplo_bus

    public
    	
    --------------------------------------------------------------
	--                       "Includes"                         --
	--------------------------------------------------------------
	with SEI;

	--------------------------------------------------------------
	--                 Especificações de Dados                  --
	--------------------------------------------------------------
    data D8
        properties
            Data_Size => 8 Bytes;
    end D8;
    
    data D4
    	properties
    		Data_Size => 4 Bytes;
    end D4;

    --------------------------------------------------------------
	--                       Barramentos                        --
	--------------------------------------------------------------

    bus MyBus
        properties
            Data_Size => 8 Bytes;
            SEI::BandWidthBudget => 512.0 KBytesps;
            SEI::BandWidthCapacity => 768.0 KBytesps;
    end MyBus;

	-- barramentos virtuais [']
    virtual bus MyVB1
        properties
            Data_Size => 16 Bytes; 
            SEI::BandWidthBudget => 384.0 KBytesps;
            SEI::BandWidthCapacity => 512.0 KBytesps;
    end MyVB1;

    virtual bus MyVB2
        properties
            Data_Size => 24 Bytes; 
            SEI::BandWidthBudget => 256.0 KBytesps;
            SEI::BandWidthCapacity => 384.0 KBytesps;
    end MyVB2;

    virtual bus MyVB3
        modes
            z1: initial mode;
            z2: mode;
        properties
	    	Data_Size => 32 Bytes in modes (z1), 64 Bytes in modes (z2);--[']
		    SEI::BandWidthBudget => 128.0 KBytesps;
        SEI::BandWidthCapacity => 256.0 KBytesps;
    end MyVB3;

    system S1
        features
            out1: out data port D8;
            out2: out data port D8;
            out3: out data port D8;
            out4: out data port D4;
    end S1;

    system S2
        features
            in1: in data port D8;
            in2: in data port D8;
            in3: in data port D8;
            in4: in data port D4;
    end S2;

	--------------------------------------------------------------
	--              Sistema Geral da Arquitetura                --
	--------------------------------------------------------------
    system top
    end top;
	---------------------- IMPLEMENTACAO --------------------------
    system implementation top.i
        subcomponents
            sub1: system s1;
            sub2: system s2;
            theBus: bus MyBus;
            
            VB1: virtual bus MyVB1;
            VB2: virtual bus MyVB2;
            VB3: virtual bus MyVB3;
        connections
            conn1: port sub1.out1 -> sub2.in1 {
                Actual_Connection_Binding => (reference (theBus));
                SEI::BandWidthBudget => 20.0 KBytesps;
            };
            conn2: port sub1.out2 -> sub2.in2 {
                Actual_Connection_Binding => (reference (VB1));
                SEI::BandWidthBudget => 40.0 KBytesps;
            };
            conn3: port sub1.out3 -> sub2.in3 {
                Actual_Connection_Binding => (reference (VB2));
                SEI::BandWidthBudget => 64.0 KBytesps;
            };
            conn4: port sub1.out4 -> sub2.in4 {
                Actual_Connection_Binding => (reference (VB3));
                SEI::BandWidthBudget => 120.0 KBytesps;
            };
        properties
            -- conexão de bus virtual
            Actual_Connection_Binding => (reference (theBus)) applies to vb1;			
            Actual_Connection_Binding => (reference (vb1)) applies to vb2;			
            Actual_Connection_Binding => (reference (vb2)) applies to vb3;			
		
            -- taxa de comunicação
            Communication_Properties::Output_Rate => [Value_Range => 800.0 .. 1000.0; Rate_Unit => PerSecond;] applies to sub1.out1;
            Communication_Properties::Output_Rate => [Value_Range => 800.0 .. 1000.0; Rate_Unit => PerSecond;] applies to sub1.out2;
            Communication_Properties::Output_Rate => [Value_Range => 800.0 .. 1000.0; Rate_Unit => PerSecond;] applies to sub1.out3;
            Communication_Properties::Output_Rate => [Value_Range => 800.0 .. 1000.0; Rate_Unit => PerSecond;] applies to sub1.out4;
    end top.i;
end Exemplo_bus;

------------------------------------------------------------------
--                        FIM DO ARQUIVO                        --
------------------------------------------------------------------