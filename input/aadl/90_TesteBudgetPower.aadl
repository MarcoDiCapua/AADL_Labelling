------------------------------------------------------------------
--                 EXEMPLO DE BUS LOAD ANALYSIS                 --
------------------------------------------------------------------
package Exemplo

    public
    	
    --------------------------------------------------------------
	--                       "Includes"                         --
	--------------------------------------------------------------
	with SEI;

	--------------------------------------------------------------
	--                 Especificações de Dados                  --
	--------------------------------------------------------------
    data SensorData
		properties
			Data_Size => 2 KByte;
	end SensorData;
	
	data CANdata
		properties
			Data_Size => 100 Bytes;
	end CANdata;

	data actuatecmd
		properties
			Data_Size => 100 Bytes;
	end actuatecmd;
	
	--------------------------------------------------------------
	--                 DEFINICAO DE ENERGIA[']                  --
	--------------------------------------------------------------
	abstract Eletricidade
	end Eletricidade;

	abstract Massa
	end Massa;

	system FornecedorEnergia
		features
			Fornecedor: out feature Eletricidade;
		properties
			SEI::GrossWeight => 1.2kg;
			SEI::PowerSupply => 40.0W applies to Fornecedor;
	end FornecedorEnergia;

	system GridEnergia
		features
			Fornecedor: in feature Eletricidade;
			Consumidor: out feature Eletricidade;
		properties
			SEI::GrossWeight => 0.9kg;
			SEI::PowerCapacity => 40.0W;
	end GridEnergia;

    --------------------------------------------------------------
	--                       Barramentos                        --
	--------------------------------------------------------------
	bus CANbus
		features
			potencia: in feature Eletricidade;
		properties
			SEI::PowerBudget => 0.8w applies to potencia;
			SEI::GrossWeight => 0.1kg;
			SEI::BandWidthCapacity => 768.0 KBytesps;
			SEI::BandWidthBudget => 512.0 KBytesps;
	end CANbus;

	--------------------------------------------------------------
	--                        DEVICES                           --
	--------------------------------------------------------------
	device sensor
		features
			potencia: in feature Eletricidade;
			canbusaccess: requires bus access CANbus;
			sensorout: out data port SensorData;
		properties
			SEI::GrossWeight => 0.18kg;
			SEI::PowerBudget => 0.45w applies to potencia;
			Period => 25ms;
			Dispatch_Protocol => Periodic;
	end sensor;

	device atuador
		features
			potencia: in feature Eletricidade;
			canbusaccess: requires bus access CANbus;
			actuatorin: in data port actuatecmd;
		properties
			SEI::GrossWeight => 0.3kg;
			SEI::PowerBudget => 0.8w applies to potencia;
			Period => 25ms;
			Dispatch_Protocol => Periodic;
	end atuador;
	
	--------------------------------------------------------------
	--                         SISTEMAS                         --
	--------------------------------------------------------------
	-- definicao do sistema
	system Sistema
	end Sistema;
	
	system DCA
		features
			sensorin: in data port SensorData;
			actuatorOut: out data port actuatecmd;
			
			canbusaccess: provides bus access CANbus;
			potencia: in feature Eletricidade;
		properties
			SEI::MIPSBudget => 2.0 mips;
			SEI::PowerBudget => 0.8w applies to potencia;
			SEI::MIPSCapacity => 2.4 mips;
	end DCA;
	
	-- Domain Control System
	system DCS
		features
			potencia: in feature Eletricidade;
			canbusaccess: provides bus access CANbus;
			sensorin: in data port SensorData;
			actuatorOut: out data port actuatecmd;
	end DCS;
	
	-- Software Communication System
	system SCS
		features
			out1: out data port CANdata;
			in1: in data port CANdata;
            
			potencia: in feature Eletricidade;
		properties
			SEI::PowerBudget => 2.5w applies to potencia;
			SEI::WeightLimit => 2.5kg;
	end SCS;
	
	--------------------------------------------------------------
	--        Implementacao de sistemas intermediarios          --
	--------------------------------------------------------------
	system implementation DCS.i
		subcomponents
			dca: system DCA;
		connections
			p1: feature potencia -> dca.potencia;
			extcb: bus access dca.canbusaccess <-> canbusaccess;
			data1: port sensorin -> dca.sensorin;
			data2: port dca.actuatorOut -> actuatorOut;
	end DCS.i;
	
	system implementation SCS.i
		subcomponents
			dcs: system DCS.i;
			sensor1: device sensor;
			atuador: device atuador;
		connections
			p1: feature potencia -> dcs.potencia;
			p2: feature potencia -> sensor1.potencia;
			p3: feature potencia -> atuador.potencia;
			
			cb1: bus access dcs.canbusaccess <-> sensor1.canbusaccess;
			cb2: bus access dcs.canbusaccess <-> atuador.canbusaccess;
			data1: feature sensor1.sensorout -> dcs.sensorin;
			data2: feature dcs.actuatorOut -> atuador.actuatorin;
	end SCS.i;
	
	--------------------------------------------------------------
	--            Implementacao do sistema Geral                --
	--------------------------------------------------------------
	system implementation Sistema.i
		subcomponents
			Bateria: system FornecedorEnergia;
			
			Grid1: system GridEnergia;
			scs1: system SCS.i;
			
			Grid2: system GridEnergia;
			scs2: system SCS.i;
			
			barramento: bus CANbus;
		connections
			psupply1: feature Bateria.Fornecedor -> Grid1.Fornecedor;
			pconsume1: feature Grid1.Consumidor -> scs1.potencia;
			
			psupply2: feature Bateria.Fornecedor -> Grid2.Fornecedor;
			pconsume2: feature Grid2.Consumidor -> scs2.potencia;
			
			conn1: port scs1.out1 -> scs2.in1 {
                Actual_Connection_Binding => (reference (barramento));
                SEI::BandWidthBudget => 20.0 KBytesps;
            };
            conn2: port scs2.out1 -> scs1.in1 {
                Actual_Connection_Binding => (reference (barramento));
                SEI::BandWidthBudget => 40.0 KBytesps;
            };
		properties
			SEI::WeightLimit => 3.0kg;
	end Sistema.i;

end Exemplo;

------------------------------------------------------------------
--                        FIM DO ARQUIVO                        --
------------------------------------------------------------------