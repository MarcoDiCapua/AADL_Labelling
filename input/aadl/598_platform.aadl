------------------------------------------------------------------
--                            ECUs                              --
------------------------------------------------------------------
package Modelo3::platform

public
	--------------------------------------------------------------
	--                       "Includes"                         --
	--------------------------------------------------------------
	with SEI;
	with emv2;
	with arp4761;

	--------------------------------------------------------------
	--                    Processador Geral                     --
	--------------------------------------------------------------
	processor ecu
		properties
			SEI::MIPSCapacity => 50.0 MIPS;
		annex EMV2 {**
			use types Modelo3::error_library;
			use behavior Modelo3::error_library::simple;
			
			error propagations
				bindings: out propagation {SoftwareFailure, HardwareFailure};
				flows
					fsoft: error source bindings {SoftwareFailure};
					fhard: error source bindings {HardwareFailure};
			end propagations;
			
			component error behavior
				events
					SoftwareError: error event;
					HardwareError: error event;
					Reset: recover event;
				transitions
					Operational -[SoftwareError]-> Failed;
					Operational -[HardwareError]-> Failed;
					Failed -[Reset]-> Operational;
				propagations
					normal: Operational -[]-> bindings {NoError};
					softfailed: Failed -[]-> bindings {SoftwareFailure};
					hardfailed: Failed -[]-> bindings {HardwareFailure};
			end component;
			
			properties
				emv2::severity => ARP4761::Major applies to SoftwareError;
				emv2::likelihood => ARP4761::Probable applies to SoftwareError;
				emv2::hazards => ([crossreference => "N/A";
						failure => "SoftwareFault";
						phases => ("all");
						description => "Software failure from the platform (OS exception, etc.)";
						comment => "Impact all components that are controlled by this software";
					]) applies to SoftwareError;
				EMV2::OccurrenceDistribution => [ProbabilityValue => 1.35e-5; Distribution => Poisson;] applies to SoftwareError;
			
				emv2::severity => ARP4761::Major applies to HardwareError;
				emv2::likelihood => ARP4761::Probable applies to HardwareError;
				emv2::hazards => ([crossreference => "N/A";
						failure => "HardwareFault";
						phases => ("all");
						description => "Hardware failure from the platform (OS exception, etc.)";
						comment => "Impact all components that are controlled by this processor";
					]) applies to HardwareError;
				EMV2::OccurrenceDistribution => [ProbabilityValue => 1.35e-5; Distribution => Poisson;] applies to HardwareError;
		**};
	end ecu;
	
	--------------------------------------------------------------
	--                 ECU com uma conexao CAN                  --
	--------------------------------------------------------------
	processor ecu_can_one_connector extends ecu
		features
			socket: requires bus access can;
	end ecu_can_one_connector;

	--------------------------------------------------------------
	--                ECU com um conector Ethernet                 --
	--------------------------------------------------------------
	processor ecu_ethernet_one_connector extends ecu
		features
			socket: requires bus access ethernet;
	end ecu_ethernet_one_connector;

	--------------------------------------------------------------
	--                ECU com duas conexoes CAN                 --
	--------------------------------------------------------------
	processor ecu_can_two_connectors extends ecu
		features
			socket1: requires bus access can;
			socket2: requires bus access can;
	end ecu_can_two_connectors;

	--------------------------------------------------------------
	--            ECU com dois conectores Ethernet              --
	--------------------------------------------------------------
	processor ecu_ethernet_two_connector extends ecu
		features
			socket1: requires bus access ethernet;
			socket2: requires bus access ethernet;
	end ecu_ethernet_two_connector;

	--------------------------------------------------------------
	--                     ESP32-DEVKIT                         --
	--------------------------------------------------------------
	
	processor ESP32_DevKit extends ecu
		--properties
		   --Largura_de_banda => 5000;
	       --Latencia_de_interrupcao => 100;
	       --Tamanho_do_buffer => 256;
	       --Velocidade_do_clock => 240;
	       --Tamanho_da_palavra_de_dados => 32;
	       --Consumo_de_energia => 2.5;
	       --Taxa_de_transferencia_de_dados => 4000;
	    features 
	    	-- MCP2515 CAN Controller
	    	socket_can1: requires bus access can;
			socket_can2: requires bus access can;
			-- ENC28J60
			socket_ethernet1: requires bus access ethernet;
			socket_ethernet2: requires bus access ethernet;
			socket_ethernet3: requires bus access ethernet;
			socket_ethernet4: requires bus access ethernet;
			-- Nativo
			socket_uart1: requires bus access UART;
			socket_uart2: requires bus access UART;
			socket_uart3: requires bus access UART;
			-- MCP2004A
			socket_lin1: requires bus access LIN;
			socket_lin2: requires bus access LIN;
	end ESP32_DevKit;
	
	--------------------------------------------------------------
	--                   RASPBERRY PI-PICO                      --
	--------------------------------------------------------------
	
	processor Raspberrypi_pico extends ecu
	    features 
	    	-- MCP2515 CAN Controller
	    	socket_can1: requires bus access can;
			socket_can2: requires bus access can;
			socket_can3: requires bus access can;
			-- ENC28J60
			socket_ethernet1: requires bus access ethernet;
			socket_ethernet2: requires bus access ethernet;
			socket_ethernet3: requires bus access ethernet;
			-- Nativo
			socket_uart1: requires bus access UART;
			socket_uart2: requires bus access UART;
			-- MCP2004A
			socket_lin1: requires bus access LIN;
			socket_lin2: requires bus access LIN;
	end Raspberrypi_pico;
	
	--------------------------------------------------------------
	--                     TIVA-C SERIES                        --
	--------------------------------------------------------------
	
	processor Tiva_cseries extends ecu
	    features 
	    	-- Nativo
	    	socket_can1: requires bus access can;
			socket_can2: requires bus access can;
			socket_can3: requires bus access can;
			socket_can4: requires bus access can;
			-- Nativo
			socket_ethernet1: requires bus access ethernet;
			-- Nativo
			socket_uart1: requires bus access UART;
			socket_uart2: requires bus access UART;
			socket_uart3: requires bus access UART;
			socket_uart4: requires bus access UART;
			-- MCP2004A
			socket_lin1: requires bus access LIN;
			socket_lin2: requires bus access LIN;
	end Tiva_cseries;
	
	--------------------------------------------------------------
	--                          CAN                             --
	--------------------------------------------------------------
	-- exemplos ja com valores, para o uso de variacoes do      --
	-- protocolo, realizar os calculos de largura de banda do   --
	-- Modelo3 pelo link:                                    --
	--                                                          --
	-- http://www.esacademy.com/en/library/calculators/can-best-and-worst-case-calculator.html
	--                                                          --
	--------------------------------------------------------------
	bus can
		properties
			Latency => 1Ms .. 1Ms;
			SEI::BandWidthCapacity => 500000.0 bitsps;
			SEI::BandWidthBudget => 500.0 bitsps;
			Transmission_Time => [Fixed => 10ms .. 30ms;
				PerByte => 1us .. 10us;];
	end can;
	
	--------------------------------------------------------------
	--                       ETHERNET                           --
	--------------------------------------------------------------
	-- exemplos ja com valores, para o uso de variacoes do      --
	-- protocolo, realizar os calculos de largura de banda do   --
	-- Modelo3 pelo link mencionado anteriormente.           --
	--                                                          --
	--------------------------------------------------------------
	bus ethernet
      properties
        Latency => 10Ms .. 10Ms;
        SEI::BandWidthCapacity => 100.0 MBytesps;
        Transmission_Time => [Fixed => 200us .. 500us;
				PerByte => 20us .. 50us;];
	end ethernet;
	
	--------------------------------------------------------------
	--                          UART                            --
	--------------------------------------------------------------
	-- exemplos ja com valores, para o uso de variacoes do      --
	-- protocolo, realizar os calculos de largura de banda do   --
	-- Modelo3 pelo link mencionado anteriormente.           --
	--                                                          --
	--------------------------------------------------------------
	bus UART
      properties
        Latency => 10Ms .. 10Ms;
        SEI::BandWidthCapacity => 115.2 KBytesps;
        Transmission_Time => [Fixed => 10ms .. 20ms;
				PerByte => 1ms .. 2ms;];
      end UART;
      
    --------------------------------------------------------------
	--                          LIN                             --
	--------------------------------------------------------------
	-- exemplos ja com valores, para o uso de variacoes do      --
	-- protocolo, realizar os calculos de largura de banda do   --
	-- Modelo3 pelo link mencionado anteriormente.           --
	--                                                          --
	--------------------------------------------------------------
	bus LIN
      properties
        Latency => 10Ms .. 15Ms;
        SEI::BandWidthCapacity => 20.0 KBytesps;
        Transmission_Time => [Fixed => 17ms .. 34ms;
				PerByte => 5ms .. 10ms;];
      end LIN;

end Modelo3::platform;

------------------------------------------------------------------
--                        FIM DO ARQUIVO                        --
------------------------------------------------------------------